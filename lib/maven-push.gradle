def ossrhUsername = findProperty('OSSRH_USERNAME')
def ossrhPassword = findProperty('OSSRH_PASSWORD')
def signingKey = findProperty('SIGNING_KEY')
def signingKeyPwd = findProperty('SIGNING_KEY_PWD')
/**
 * IS_SNAPSHOT_RELEASE property will be automatically added to the root gradle.properties file by the CI pipeline, depending on the GitHub branch.
 * A snapshot release is generated for every pull request merged or commit made into an epic branch.
 */
def isSnapshotRelease = findProperty('IS_SNAPSHOT_RELEASE') == 'true'
def libVersion = isSnapshotRelease ? "${findProperty('VERSION_NAME')}-SNAPSHOT" : findProperty('VERSION_NAME')

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                // You can then customize attributes of the publication as shown below.
                groupId = "com.what3words"
                artifactId = "w3w-android-wrapper"
                version = libVersion

                pom {
                    name = "w3w-android-wrapper"
                    description = 'Android library for what3words REST API.'
                    url = 'https://github.com/what3words/w3w-android-wrapper'
                    licenses {
                        license {
                            name = 'The MIT License (MIT)'
                            url = 'https://github.com/what3words/w3w-android-wrapper/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'what3words'
                            name = 'what3words'
                            email = 'development@what3words.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/what3words/w3w-android-wrapper.git'
                        developerConnection = 'scm:git:ssh://git@github.com:what3words/w3w-android-wrapper.git'
                        url = 'https://github.com/what3words/w3w-android-wrapper/tree/master'
                    }
                }
            }
        }
        repositories {
            maven {
                name = "sonatype"

                def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                url = libVersion.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(signingKey, signingKeyPwd)
    sign publishing.publications
}